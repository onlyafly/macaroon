;;;;;;;;;;;;;;;;;;;;;;;;
; mtest test framework ;
;;;;;;;;;;;;;;;;;;;;;;;;

(def _mtest_tests '())

(defn _mtest_runtests (tests)
  (cond
    (= tests '()) nil
    else (let (test (first tests)
               more-tests (rest tests)
               test-name (first test)
               test-fn (first (rest test))
               result (test-fn))
           (begin
             (cond
               (= result 'PASS) (print ".")
               else (println
                      (concat
                        "" \newline \newline
                        "Test '" test-name "' failed:" \newline
                        result \newline)))
             (_mtest_runtests more-tests)))))

;;;;;;;;;; External API

;; (defmtest "Sample Test"
;;   pred1 pred2 predn...)
;; =>
;; (update! _mtest_tests
;;          (cons (list "Sample Test" (fn () (begin pred1 pred2 predn...)))
;;                _mtest_tests))
;;
(defmacro defmtest (name &rest preds)
  (list 'update! '_mtest_tests
    (list 'cons
      (list 'list name
        (list 'fn '()
          (cons 'begin preds)))
      '_mtest_tests)))

(defn mt= (actual expected)
  (if (= actual expected)
    'PASS
    (concat
      "    expected: <" expected ">" \newline
      "    actual:   <" actual ">")))

(defn mtest-start ()
  (println "***** Running tests (mtest) *****")
  (_mtest_runtests _mtest_tests)
  (println "")
  (println "***** Completed test run (mtest) *****"))